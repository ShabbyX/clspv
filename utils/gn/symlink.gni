# Creates a symlink
# Args:
#   source: Path to link to
#   output: Where to create the symlink

template("symlink_dir") {
  action(target_name) {
    forward_variables_from(invoker, [ "deps" ])
    stamp = "$target_gen_dir/" + get_path_info(invoker.output, "file") +
            target_name + ".stamp"

    outputs = [ stamp ]
    script = "//utils/gn/scripts/symlink_or_copydir.py"

    args = [
      "--stamp",
      rebase_path(stamp, root_build_dir),
      rebase_path(invoker.source),
      rebase_path(invoker.output),
    ]
  }
}
